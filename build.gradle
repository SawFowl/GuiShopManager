plugins {
	id("java")
	id("eclipse")
	id("org.spongepowered.gradle.plugin") version "2.0.2"
}

group = "sawfowl.guishopmanager"
version = "${major}.${minor}.${patch}-${api}-${suffix}"

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}
compileJava.options.compilerArgs << "-Xmaxerrs" << "9999"

repositories {
	mavenCentral()
	maven {
		name = "Sponge"
		url = "https://repo.spongepowered.org/repository/maven-public"
	}
	maven { 
		name = "JitPack"
		url 'https://jitpack.io' 
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

dependencies {
	implementation "org.spongepowered:spongeapi:${api}"
	implementation 'org.spongepowered:spongevanilla:1.19.4-10.0.0-RC1397:universal'
	implementation "com.github.SawFowl:LocaleAPI:${localeapi}"
	implementation "com.github.SawFowl:CommandPack:${commandpack}"
	//implementation fileTree(dir: 'libs', include: '*.jar')
}

sponge {
	apiVersion("${api}")
	loader {
		name("java_plain")
		version("1.0")
	}
	license("GPL-3.0")
	plugin("guishopmanager") {
		displayName(project.name.capitalize())
		version("${project.version}")
		entrypoint("${project.group}.${rootProject.name}")
		description("Plugin for create chest shops.")
		contributor("SawFowl") {
			description("Lead Developer")
		}
		links {
			homepage("https://ore.spongepowered.org/Semenkovsky_Ivan/GuiShopManager")
			source("https://github.com/SawFowl/GuiShopManager")
			issues("https://github.com/SawFowl/GuiShopManager/issues")
		}
		dependency("spongeapi") {
			optional(false)
			loadOrder("after")
			version("8.0.0")
		}
		dependency("localeapi") {
			optional(false)
			loadOrder("after")
			version("2.6.0")
		}
		dependency("commandpack") {
			optional(false)
			loadOrder("after")
			version("${commandpack}")
		}
		dependency("mysqldriver") {
			optional(true)
			loadOrder("after")
			version("1.0.0")
		}
		dependency("h2driver") {
			optional(true)
			loadOrder("after")
			version("1.0.0")
		}
	}
}
